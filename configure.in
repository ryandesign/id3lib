# $Id$

# Copyright (C) 1999 Scott Thomas Haug <sth2@cs.wustl.edu>
#  
# This file is free software; as a special exception the author gives
# unlimited permission to copy and/or distribute it, with or without 
# modifications, as long as this notice is preserved.
# 
# This program is distributed in the hope that it will be useful, but
# WITHOUT ANY WARRANTY, to the extent permitted by law; without even the
# implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.

AC_INIT(reconf)
AM_CONFIG_HEADER(config.h)

id3lib_version=3
id3lib_revision=6
id3lib_patch=0

id3lib_release=$id3lib_version.$id3lib_revision.$id3lib_patch
id3lib_name=id3lib
id3lib_date="$Date$UNDEFINED"

AM_INIT_AUTOMAKE($id3lib_name,$id3lib_release)
lt_ver=`expr $id3lib_version + $id3lib_revision`
lt_rev=$id3lib_patch
lt_age=$id3lib_revision
AC_SUBST(lt_ver)
AC_SUBST(lt_rev)
AC_SUBST(lt_age)

dnl Checks for programs
AC_PROG_CXX
AC_PROG_CXXCPP
AM_PROG_LIBTOOL
AC_PROG_RANLIB

dnl Checks for libraries.
AC_CHECK_LIB(z,uncompress,,
  AC_MSG_ERROR([id3lib requires zlib to process compressed frames]))

dnl Checks for header files.
AC_HEADER_STDC
AC_HAVE_HEADERS(                \
  ctype.h                       \
  limits.h                      \
  memory.h                      \
  stdio.h                       \
  unistd.h                      \
  wchar.h                       \
  zlib.h                        \
  ,,AC_MSG_ERROR([Missing a vital header file for id3lib])
)
AC_LANG_SAVE
AC_LANG_CPLUSPLUS
AC_HAVE_HEADERS(                \
  iostream.h                    \
  ,,AC_MSG_ERROR([Missing a vital header file for id3lib])
)

dnl Check for functions.
AC_FUNC_MEMCMP
AC_HAVE_FUNCS(
  ftruncate                     \
  ,,AC_MSG_ERROR([Missing a vital function for id3lib])
)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_C_INLINE
AC_SIZE_T

dnl
dnl Checks with local macros
dnl

dnl Checks for the portability of certain c++ features: the bool type and
dnl for-loop scoping
LF_CXX_PORTABILITY

dnl Provides a --with-warnings option to the configure script that turns on
dnl compiler warnings.  Must be used AFTER configuring ALL compilers.
LF_SET_WARNINGS

dnl ACCONFIG TEMPLATE
dnl 
dnl /* config.h defines these preprocesser symbols to be used by id3lib for
dnl  * determining internal versioning information.  The intent is that these
dnl  * macros will be made available int the library via functions or static
dnl  * methods.
dnl  */
dnl /* Defines which version of id3lib is being used (int) */
dnl #undef ID3LIB_VERSION
dnl /* Defines which revision of id3lib is being used (int) */
dnl #undef ID3LIB_REVISION
dnl /* Defines which patch of id3lib is being used (int) */
dnl #undef ID3LIB_PATCH
dnl /* The date of this id3lib release */
dnl #undef ID3LIB_DATE
dnl /* The identifying string of this id3lib */
dnl #undef ID3LIB_FULLNAME
dnl END ACCONFIG

AC_DEFINE_UNQUOTED(ID3LIB_VERSION,  ($id3lib_version))
AC_DEFINE_UNQUOTED(ID3LIB_REVISION, ($id3lib_revision))
AC_DEFINE_UNQUOTED(ID3LIB_PATCH,    ($id3lib_patch))
AC_DEFINE_UNQUOTED(ID3LIB_DATE,     "$id3lib_date")
AC_DEFINE_UNQUOTED(ID3LIB_FULLNAME, "$id3lib_name-$id3lib_release")

AC_OUTPUT(                      \
  Makefile                      \
  doc/Makefile                  \
  doc/html/Makefile             \
  doc/man/Makefile              \
  doc/latex/Makefile            \
  doc/texinfo/Makefile          \
  m4/Makefile                   \
  src/Makefile                  \
  src/id3/Makefile              \
  src/examples/Makefile         \
)

# $Log$
# Revision 1.7  1999/11/30 14:25:49  scott
# Generalized versioning mechanism so it only has to be changed in this
# file to be propagated throughout rest of source code.  Used a hack to
# make libtool version coincide with release versioning.  This is
# expressly warned against in the libtool docs, so it might be changed.
# However, it also seems to be a common practice amongst other library
# writers, so I'm torn.  Added versioning macro definitions normally
# found in version.h.  Probably not the best place for this...
#